#!/usr/bin/tclsh
# -*- mode: Tcl; tab-width: 8; -*-
#
# Copyright (C) 2011, 2012 by Roger E Critchlow Jr, Santa Fe, NM, USA.
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
# 

#
# just make some noise
#
set script [expr { ! [catch {file readlink [info script]} link] ? $link : [info script]}]
lappend auto_path [file join [file dirname $script] .. lib]

package require Tk
package require sdrkit::noise

array set data {
    server default
    name noise
    length 320
    level -30.0
    max-level 0.0
    min-level -160.0
}

proc set-level {v} {
    $::data(name) configure -level $v
    set ::data(level-label) [format %.1f $v]
}

proc shutdown {w} {
    if {$w eq {.}} {
	rename $::data(name) {}
    }
}

proc main {argv} {
    foreach {option value} $argv {
	switch -- $option {
	    -n - -name - --name { set ::data(name) $value }
	    -s - -server - --server { set ::data(server) $value }
	    -l - -level - --level { set ::data(level) $value }
	    -min-level - --min-level { set ::data(min-level) $value }
	    -max-level - --max-level { set ::data(max-level) $value }
	    default { error "unknown option \"$option\"" }
	}
    }

    sdrkit::noise $::data(name)
    wm title . $::data(name)
    set-level $::data(level)

    grid [ttk::label .level-l -textvar ::data(level-label) -width 10 -anchor e] -row 0 -column 0
    grid [ttk::label .level-u -text dB] -row 0 -column 1
    grid [ttk::scale .level-s -from $::data(max-level) -to $::data(min-level) -command set-level -variable ::data(level) -length $::data(length)] -row 0 -column 2 -sticky ew
    grid columnconfigure . 2 -weight 100
    bind . <Destroy> [list shutdown %W]
}

main $argv