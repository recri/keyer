#!/usr/bin/tclsh
# -*- mode: Tcl; tab-width: 8; -*-
#
# Copyright (C) 2011, 2012 by Roger E Critchlow Jr, Santa Fe, NM, USA.
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
# 

#
# an sdrkit embeddable iq-balance component
#

#
# find the script file
# and add the lib directory to the automatic package search path
#
set script [expr { ! [catch {file readlink [info script]} link] ? $link : [info script]}]
lappend auto_path [file join [file dirname $script] .. lib]

#
# require Tk for graphical UI and communications
# sdrtcl::iq-balance to implement the component
#
package require Tk
package require sdrtcl::iq-balance

#
# on the fly implementation of control interface
#
package require snit

namespace eval sdrkit {}

snit::type sdrkit::iq-balance {    
    option -server -default default -readonly true
    option -sample-rate -default 0 -readonly true
    option -name -default sdr-iq-balance -readonly true
    option -minsizes -default {100 200} -readonly true
    option -weights -default {1 3} -readonly true
    option -control -default {} -readonly true
    option -enable -default 1 -configuremethod Configure
    option -activate -default 1 -configuremethod Configure

    option -phase -default 0 -configuremethod Configure
    option -min-phase -default 90 -configuremethod Configure
    option -max-phase -default -90 -configuremethod Configure
    option -gain -default 0 -configuremethod Configure
    option -min-gain -default -6.0 -configuremethod Configure
    option -max-gain -default  6.0 -configuremethod Configure

    variable data -array {
	gain-label {}
	phase-label {}
    }

    constructor {args} {

	$self configure {*}$args

	wm title . $options(-name)
	tk appname $options(-name)

	sdrtcl::iq-balance $options(-name) -server $options(-server)

	if {$options(-control) ne {}} {
	    set options(-enable) 0
	    set options(-activate) 0
	    if {[$options(-name) is-active]} { $options(-name) deactivate }
	} else {
	    set options(-enable) 1
	    set options(-activate) 1
	    if { ! [$options(-name) is-active]} { $options(-name) activate }
	}

	set data(pi) [tcl::mathfunc::atan2 0 -1]

	ttk::label .gain-l -textvar [myvar data(gain-label)] -width 10 -anchor e
	ttk::scale .gain-s -from $options(-max-gain) -to $options(-min-gain) -command [mymethod Set gain] -variable [myvar options(-gain)]
	$self Set gain $options(-gain)
	grid .gain-l .gain-s -sticky ew

	ttk::label .phase-l -textvar [myvar data(phase-label)] -width 10 -anchor e
	ttk::scale .phase-s -from $options(-min-phase) -to $options(-max-phase) -command [mymethod Set phase] -variable [myvar options(-phase)]
	$self Set phase $options(-phase)
	grid .phase-l .phase-s -sticky ew

	set col -1
	foreach ms $options(-minsizes) wt $options(-weights) {
	    grid columnconfigure . [incr col] -minsize $ms -weight $wt
	}
    }
    destructor {
	catch {$options(-name) deactivate}
	catch {rename $options(-name) {}}
    }
    method Configure {opt val} {
	set options($opt) $val
	switch -- $opt {
	    -activate { $options(-name) [expr {$val?{activate}:{deactivate}}] }
	    -min-phase { set options($opt) [expr {max($options($opt),-$options(-sample-rate)/4.0)}] }
	    -max-phase { set options($opt) [expr {min($options($opt), $options(-sample-rate)/4.0)}] }
	}
    }
    method {Set gain} {v} {
	$options(-name) configure -linear-gain [expr {10.0**($v/20)}]
	set data(gain-label) [format {%.1f dBFS} $v]
    }
    method {Set phase} {v} {
	$options(-name) configure -sine-phase [expr {sin(2*$data(pi)*$v/360.0)}]
	set data(phase-label) [format {%.1f Deg} $v]
    }
}

sdrkit::iq-balance ::sdrkit {*}$argv
