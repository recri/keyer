#!/usr/bin/tclsh
# -*- mode: Tcl; tab-width: 8; -*-
#
# Copyright (C) 2011, 2012 by Roger E Critchlow Jr, Santa Fe, NM, USA.
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
# 

set script [expr { ! [catch {file readlink [info script]} link] ? $link : [info script]}]
lappend auto_path [file join [file dirname $script] .. lib]

package require Tk
package require capture
#package require scope
#package require keyer-scope

# package provide scope 1.0.0
namespace eval ::scope {
    array set default_data {
	-height 200
	-y-scale 2.0
	-y-origin 1.0
	-x-scale 1.0
	-x-origin 0.0
    }
}

proc ::scope::configure {w args} {
    upvar #0 ::scope::$w data
    foreach {option value} $args {
	switch -- $option {
	    -height {
		set data(-y-scale) [expr {-0.75*$value/2.0}]
		set data(-y-origin) [expr {$value/2.0}]
	    }
	    -x-scale {
		if {[info exists data(-x-scale)]} {
		    $w scale all 0 0 [expr {$value/$data(-x-scale)}] 1
		}
	    }
	    -x-origin {
		if {[info exists data(-x-origin)]} {
		    $w move all [expr {$value-$data(-x-origin)}] 0
		}
	    }
	    default {
	    }
	}
	set data($option) $value
    }
    catch {$w configure -scrollregion [$w bbox all]}
}

proc ::scope::start-iq {w sx} {
    upvar #0 ::scope::$w data
    catch {unset data(frame)}
    catch {unset data(chunk)}
    set data(i-xy) {}
    set data(q-xy) {}
    catch {$w delete all}
    update-iq $w 0 [binary format f* [lrepeat 1024 0.0]]
}

proc ::scope::update-iq {w frame iq} {
    upvar #0 ::scope::$w data

    set skip 0
    if { ! [info exists data(frame)]} {
	set data(frame) $frame
	set data(chunk) 0
    } elseif {$frame < $data(frame)} {
	set skip [expr {$data(frame)-$frame}]
	# puts "overlapped $skip samples"
    } elseif {$frame > $data(frame)} {
	puts "missed [expr {$frame-$data(frame)}] samples"
    }

    binary scan $iq f* samples
    set n [llength $samples]
    set isamples [lrange $samples $skip [expr {$n/2-1}]]
    #set qsamples [lrange $samples [expr {$n/2+$skip}] end]
    incr frame $skip

    set ixy {}
    #set qxy {}
    #foreach i $isamples q $qsamples {}
    foreach i $isamples {
	lappend ixy $frame $i
	#lappend qxy $frame $q
	incr frame
    }
    set data(frame) $frame

    set chunk [incr data(chunk)]
    #$w create line $qxy -fill \#7fff7f -tags [list capture-q-$chunk chunk-$chunk]
    $w create line $ixy -fill white -tags [list capture-i-$chunk chunk-$chunk]

    $w scale chunk-$chunk 0 0 $data(-x-scale) $data(-y-scale)
    $w move chunk-$chunk 0 $data(-y-origin)
    $w configure -scrollregion [$w bbox all]
    #puts "update-iq $w $frame [string length $iq]"
}

proc ::scope::start-event {w sx} {
    upvar #0 ::scope::$w data
    set data(frame) 0
    set data(midi-xy) {0 -1}
    catch {$w delete all}
    $w create line 0 0 0 0 -fill white -tags capture-midi
}

proc ::scope::update-event {w frame y} {
    upvar #0 ::scope::$w data
    lappend data(midi-xy) $frame [expr {-$y}] $frame $y
    $w coords capture-midi $data(midi-xy)
    $w scale all 0 0 $data(-x-scale) $data(-y-scale)
    $w move all 0 $data(-y-origin)
    $w configure -scrollregion [$w bbox all]
}

proc ::scope::stop {w ex} {
    $w create line $ex 0 $ex 0 -fill white
}
proc ::scope::defaults {} {
    return [array get ::scope::default_data]
}

proc ::scope::scope {w args} {
    upvar #0 ::scope::$w data
    ::scope::configure $w {*}[::scope::defaults]
    ::scope::configure $w {*}$args
    return [canvas $w -height $data(-height) -bg black]
}

proc ::scope {w args} {
    return [::scope::scope $w {*}$args]
}

# package provide time-scale 1.0.0
namespace eval ::time-scale {
    array set default_data {
	-height 20
	-scale 1.0
	-offset 0.0
    }
}

proc ::time-scale::update {w args} {
}

proc ::time-scale::configure {w args} {
}

proc ::time-scale::defaults {} {
    return [array get ::time-scale::default_data]
}

proc ::time-scale::time-scale {w args} {
    upvar #0 ::time-scale::$w data
    array set data [::time-scale::defaults]
    array set data $args
    return [canvas $w -height $data(-height) -bg black]
}


proc ::time-scale {w args} {
    return [::time-scale::time-scale $w {*}$args]
}

# package provide keyer-scope 1.0.0

namespace eval ::keyer-scope {
    array set default_data {
	-chan 1
	-note 0
	-zoom 0.01
	-origin 0.0
	-scroll 0.0
	-connect {}
	-period 50
	-size 4096
    }
}

proc ::keyer-scope::configure {w args} {
    upvar #0 ::keyer-scope::$w data
    foreach {option value} $args {
	switch -- $option {
	    -zoom {
		set data(-zoom) $value
		set srate [sdrkit::jack sample-rate]
		set scale [expr {$data(-zoom)/$srate}]
		::scope::configure $w.iq -x-scale $scale
		::scope::configure $w.keyer -x-scale $scale
		::scope::configure $w.key1 -x-scale $scale
		::scope::configure $w.key0 -x-scale $scale
		::keyer-scope::scrollregion $w
	    }
	    default { set data($option) $value 	}
	}
    }
}

proc ::keyer-scope::update-iq {w frame binary} {
    # puts "keyer-scope update-iq $w $frame [string length $binary]"
    set w [winfo parent $w]
    upvar #0 ::keyer-scope::$w data
    set frame [expr {$frame-$data(start-frame)}]
    if {$frame >= 0} {
	::scope::update-iq $w.iq $frame $binary
	::keyer-scope::scrollregion $w
    }
}

proc ::keyer-scope::update-midi {wkey events} {
    # puts "keyer-scope update-midi $wkey [llength $events]"
    set w [winfo parent $wkey]
    upvar #0 ::keyer-scope::$w data
    foreach item $events {
	foreach {frame event} $item {
	    binary scan $event ccc cmd note velocity
	    set chan [expr {($cmd&0xF)+1}]
	    set cmd [expr {($cmd>>4)&0xF}]
	    if {$chan == $data(-chan) && ($cmd == 8 || $cmd == 9)} {
		if {$note == $data(-note)} {
		    ::scope::update-event $wkey [expr {$frame-$data(start-frame)}] [expr {2*($cmd-8)-1}]
		} elseif {$note == $data(-note)+1} {
		    ::scope::update-event $w.key1 [expr {$frame-$data(start-frame)}] [expr {2*($cmd-8)-1}]
		}
	    }
	}
    }
}

proc ::keyer-scope::window-configure {w cw width height} {
    if {$w ne $cw} return
    # puts "keyer-scope::window-configure $w $cw $width $height"
    upvar #0 ::keyer-scope::$w data
    # ::capture::configure $w -size $width
    set srate [sdrkit::jack sample-rate]
    set scale [expr {$data(-zoom)/$srate}]
    ::scope::configure $w.iq -x-scale $scale
    ::scope::configure $w.keyer -x-scale $scale
    ::scope::configure $w.key1 -x-scale $scale
    ::scope::configure $w.key0 -x-scale $scale
    ::keyer-scope::scrollregion $w
}

proc ::keyer-scope::window-destroy {w cw} {
    if {$w ne $cw} return
    upvar #0 ::keyer-scope::$w data
    ::capture::destroy $w
    ::scope::destroy $w.iq
    ::scope::destroy $w.keyer
    ::scope::destroy $w.key1
    ::scope::destroy $w.key0
}

proc ::keyer-scope::defaults {} {
    return [array get ::keyer-scope::default_data]
}

proc ::keyer-scope::start {w} {
    upvar #0 ::keyer-scope::$w data
    set data(start-frame) [sdrkit::jack frame-time]
    ::scope::start-iq $w.iq 0
    ::scope::start-event $w.keyer 0
    ::scope::start-event $w.key0 0
    ::scope::start-event $w.key1 0
    ::capture::start $w.iq
    ::capture::start $w.keyer
    ::capture::start $w.key0
}

proc ::keyer-scope::stop {w} {
    upvar #0 ::keyer-scope::$w data
    set data(last-frame) [sdrkit::jack frame-time]
    set frames [expr {$data(last-frame)-$data(start-frame)}]
    ::capture::stop $w.iq
    ::capture::stop $w.keyer
    ::capture::stop $w.key0
    ::scope::stop $w.iq $frames
    ::scope::stop $w.keyer $frames
    ::scope::stop $w.key0 $frames
    ::scope::stop $w.key1 $frames
    #puts "iq: [lrange [$w.iq coords all] 0 10]"
    #puts "keyer: [lrange [$w.keyer coords all] 0 10]"
    #puts "key0: [lrange [$w.key0 coords all] 0 10]"
    #puts "key1: [lrange [$w.key1 coords all] 0 10]"
}

proc ::keyer-scope::xview {w args} {
    $w.iq xview {*}$args
    $w.keyer xview {*}$args
    $w.key0 xview {*}$args
    $w.key1 xview {*}$args
    #puts "keyer-scope::xview $w $args"
}

proc ::keyer-scope::scrollregion {w} {
    if {[catch {
	$w.x set {*}[$w.iq xview]
    } error]} {
	puts $error
    }
}

proc ::keyer-scope::keyer-scope {w args} {
    upvar #0 ::keyer-scope::$w data
    array set data [::keyer-scope::defaults]
    array set data $args
    ttk::panedwindow $w -orient vertical
    $w add [::scope $w.iq -height 50] -weight 1
    $w add [::scope $w.keyer -height 50] -weight 1
    $w add [::scope $w.key1 -height 50] -weight 1
    $w add [::scope $w.key0 -height 50] -weight 1
    $w add [::time-scale $w.t -height 20] -weight 0
    $w add [ttk::scrollbar $w.x -orient horizontal -command [list ::keyer-scope::xview $w]] -weight 0
    # period and size must add up to most of the data
    set data(sample-rate) [sdrkit::jack sample-rate]
    set data(buffer-size) [sdrkit::jack buffer-size]
    # whatever rate I capture at, I should capture
    # a little sooner and expect to get overlaps
    for {set n 1} {1} {set n [expr {$n+$n}]} {
	set ns [expr {$n*$data(buffer-size)}]
	set nms [expr {1000*$ns/$data(sample-rate)}]
	if {$nms > 100} break
	set data(capture-samples) $ns
	set data(capture-millis) $nms
    }
    # puts "capture $data(capture-samples) samples every $data(capture-millis) ms => [expr {1000*$data(capture-samples)/$data(capture-millis)}] samples/second vs $data(sample-rate)"
    incr data(capture-samples) $data(capture-samples)
    ::capture::iq $w.iq -period $data(capture-millis) -size $data(capture-samples) -client ::keyer-scope::update-iq -connect iambic_tone
    ::capture::midi $w.keyer -period 200 -client ::keyer-scope::update-midi -connect iambic:midi_out
    ::capture::midi $w.key0 -period 200 -client ::keyer-scope::update-midi -connect system:midi_capture_1
    bind . <Configure> [list ::keyer-scope::window-configure $w %W %w %h]
    bind . <Destroy> [list ::keyer-scope::window-destroy $w %W]
    return $w
}

proc keyer-scope {w args} {
    return [keyer-scope::keyer-scope $w {*}$args]
}

array set data {
    state 0
    state-text {capture start}
    log-zoom 0
    zoom 1
    zoom-label 1.0
}

proc swap-state {} {
    global data
    if {$data(state)} {
	::keyer-scope::stop .k
	set data(state) 0
	.c.go configure -text start
    } else {
	::keyer-scope::start .k
	set data(state) 1
	.c.go configure -text stop
    }
}

proc zoom {step} {
    global data
    incr data(log-zoom) $step
    set frac [lindex {1 2.5 5} [expr {$data(log-zoom)%3}]]
    set expo [expr {pow(10,$data(log-zoom)/3)}]
    set data(zoom) [expr {$frac*$expo}]
    set data(zoom-label) [format %7.1e $data(zoom)]
    keyer-scope::configure .k -zoom $data(zoom)
}

proc main {argv} {
    global data
    wm title . {keyer-scope}
    pack [keyer-scope .k {*}$argv] -side top -fill both -expand true
    zoom 0
    pack [ttk::frame .c] -side top
    pack [ttk::button .c.go -text start -command swap-state] -side left
    pack [ttk::button .c.zoom-in -text {-t} -command {zoom -1}] -side left
    pack [ttk::label .c.zoom -textvariable data(zoom-label) -width 8] -side left
    pack [ttk::button .c.zoom-out -text {+t} -command {zoom +1}] -side left
    pack [ttk::button .c.quit -text quit -command {destroy .}] -side left
}

main $argv