#!/usr/bin/tclsh
# -*- mode: Tcl; tab-width: 8; -*-
#
# Copyright (C) 2011, 2012 by Roger E Critchlow Jr, Santa Fe, NM, USA.
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
# 

set script [expr { ! [catch {file readlink [info script]} link] ? $link : [info script]}]
lappend auto_path [file join [file dirname $script] .. lib]

package require Tk
package require sdrkit::filter-complex-bandpass

array set data {
    server default
    name bandpass
    length 100
    filter-length 15
    center 0.0
    min-center -10000
    max-center  10000
    width 10000.0
    min-width 20
    max-width 25000
}

proc draw-filter {w width center length} {
    if { ! [winfo exists $w]} return
    set wd [winfo width $w]
    set ht [winfo height $w]
    set f0 [expr {0.5+(($center/32000.0)*0.5)}]
    set lo [expr {$f0-(($width/(2*32000.0))*0.5)}]
    set hi [expr {$f0+(($width/(2*32000.0))*0.5)}]
    catch {$w delete all}
    $w create line 0.5 0.0 0.5 1.0 -fill grey
    $w create line $lo 1.0 $lo 0.2 $hi 0.2 $hi 1.0 -fill black
    $w scale all 0 0 $wd $ht
}

proc set-filter {} {
    draw-filter .c $::data(width) $::data(center) $::data(filter-length)
    $::data(name) configure -low [expr {$::data(center)-$::data(width)/2}] -high [expr {$::data(center)+$::data(width)/2}]
}

proc set-width {v} {
    set ::data(width-label) [format %6.0f $v]
    set-filter
}

proc set-center {v} {
    set ::data(center-label) [format %6.0f $v]
    set-filter
}

proc set-length {args} {
    if {[llength $args] == 1} {
	set v [lindex $args 0]
    } else {
	set v $::data(filter-length)
    }
    $::data(name) configure -length $v
    set ::data(filter-length-label) $v
}

proc shutdown {w} {
    if {$w eq {.}} {
	rename $::data(name) {}
    }
}

proc main {argv} {
    foreach {option value} $argv {
	switch -- $option {
	    -n - -name - --name { set ::data(name) $value }
	    -s - -server - --server { set ::data(server) $value }
	    -w - -width - --width { set ::data(width) $value }
	    -c - -center - --center { set ::data(center) $value }
	    -f - -filter-length - --filter-length { set ::data(filter-length) $value }
	    default { error "unknown option \"$option\"" }
	}
    }

    sdrkit::filter-complex-bandpass $::data(name)
    wm title . "sdrkit:$::data(name)"
    set-center $::data(center)
    set-width $::data(width)
    set-length $::data(filter-length)
    set row 0
    grid [canvas .c -width 128 -height 128] -row $row -column 0 -columnspan 3
    incr row
    grid [ttk::label .width-l -textvar ::data(width-label) -width 10 -anchor e] -row $row -column 0
    grid [ttk::label .width-u -text Hz] -row $row -column 1
    grid [ttk::scale .width-s -from $::data(min-width) -to $::data(max-width) -command set-width -variable ::data(width) -length $::data(length)] -row $row -column 2 -sticky ew
    incr row
    grid [ttk::label .center-l -textvar ::data(center-label) -width 10 -anchor e] -row $row -column 0
    grid [ttk::label .center-u -text Hz] -row $row -column 1
    grid [ttk::scale .center-s -from $::data(min-center) -to $::data(max-center) -command set-center -variable ::data(center) -length $::data(length)] -row $row -column 2 -sticky ew
    incr row
    grid [ttk::label .length-l -textvar ::data(filter-length-label) -width 10 -anchor e] -row $row -column 0
    grid [ttk::spinbox .length-s -from 3 -to 127 -increment 2 -command set-length -textvariable ::data(filter-length)] -row $row -column 2 -sticky ew
    incr row
    grid columnconfigure . 2 -weight 100
    bind . <Destroy> [list shutdown %W]
}

main $argv
