#!/usr/bin/tclsh
# -*- mode: Tcl; tab-width: 8; -*-
#
# Copyright (C) 2011, 2012 by Roger E Critchlow Jr, Santa Fe, NM, USA.
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
# 

#
# an sdrkit embeddable gain component
#

#
# find the script file
# and add the lib directory to the automatic package search path
#
set script [expr { ! [catch {file readlink [info script]} link] ? $link : [info script]}]
lappend auto_path [file join [file dirname $script] .. lib]

#
# require Tk for graphical UI and communications
# sdrtcl::gain to implement the component
#
package require Tk
package require sdrtcl::gain

#
# on the fly implementation of control interface
#
package require snit

namespace eval sdrkit {}

snit::type sdrkit::gain {    
    option -server -default default -readonly true
    option -name -default sdr-gain -readonly true
    option -minsizes -default {100 200} -readonly true
    option -weights -default {1 3} -readonly true
    option -control -default {} -readonly true
    option -enable -default 1 -configuremethod Configure
    option -activate -default 1 -configuremethod Configure

    option -gain -default -30.0 -configuremethod Configure
    option -min-gain -default -160.0 -configuremethod Configure
    option -max-gain -default  160.0 -configuremethod Configure

    variable data -array {
	gain-label {}
    }

    constructor {args} {

	$self configure {*}$args

	wm title . $options(-name)
	tk appname $options(-name)

	sdrtcl::gain $options(-name) -server $options(-server) -gain $options(-gain)

	if {$options(-control) ne {}} {
	    set options(-enable) 0
	    set options(-activate) 0
	    if {[$options(-name) is-active]} { $options(-name) deactivate }
	} else {
	    set options(-enable) 1
	    set options(-activate) 1
	    if { ! [$options(-name) is-active]} { $options(-name) activate }
	}

	ttk::label .gain-l -textvar [myvar data(gain-label)] -width 10 -anchor e
	ttk::scale .gain-s -from $options(-max-gain) -to $options(-min-gain) -command [mymethod Set gain] -variable [myvar options(-gain)]
	$self Set gain $options(-gain)
	grid .gain-l .gain-s -sticky ew

	set col -1
	foreach ms $options(-minsizes) wt $options(-weights) {
	    grid columnconfigure . [incr col] -minsize $ms -weight $wt
	}
    }
    destructor {
	catch {$options(-name) deactivate}
	catch {rename $options(-name) {}}
    }
    method Configure {opt val} {
	set options($opt) $val
	switch -- $opt {
	    -activate { $options(-name) [expr {$val?{activate}:{deactivate}}] }
	    default {}
	}
    }
    method {Set gain} {v} {
	$options(-name) configure -gain $v
	set data(gain-label) [format {%.1f dBFS} $v]
    }
}

sdrkit::gain ::sdrkit {*}$argv
