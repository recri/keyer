#!/usr/bin/tclsh
# -*- mode: Tcl; tab-width: 8; -*-
#
# Copyright (C) 2011, 2012 by Roger E Critchlow Jr, Santa Fe, NM, USA.
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
# 

#
# just make an oscillator
#
set script [expr { ! [catch {file readlink [info script]} link] ? $link : [info script]}]
lappend auto_path [file join [file dirname $script] .. lib]

package require Tk
package require sdrkit::oscillator
package require sdrkit::jack

array set data {
    server default
    name osc
    length 320
    freq 600.0
    min-freq 1.0
    max-freq 23500
    gain -30.0
    min-gain -160.0
    max-gain 0.0
}

proc set-gain {v} {
    $::data(name) configure -gain $v
    set ::data(gain-label) [format %.1f $v]
}

proc set-freq {v} {
    $::data(name) configure -freq $v
    set ::data(freq-label) [format %.1f $v]
}

proc shutdown {w} {
    if {$w eq {.}} {
	rename $::data(name) {}
    }
}

proc main {argv} {
    if {$::data(max-freq) > [sdrkit::jack sample-rate]/4} {
	set ::data(max-freq) [expr {[sdrkit::jack sample-rate]/4.01}]
    }
    foreach {option value} $argv {
	switch -- $option {
	    -n - -name - --name { set ::data(name) $value }
	    -s - -server - --server { set ::data(server) $value }
	    -l - -length - --length { set ::data(length) $value }
	    -f - -freq - --freq - -frequency - --frequency { set ::data(freq) $value }
	    -min-freq - --min-freq { set ::data(min-freq) $value }
	    -max-freq - --max-freq { set ::data(max-freq) $value }
	    -g - -gain - --gain { set ::data(gain) $value }
	    -min-gain - --min-gain { set ::data(min-gain) $value }
	    -max-gain - --max-gain { set ::data(max-gain) $value }
	    default { error "unknown option \"$option\"" }
	}
    }

    sdrkit::oscillator $::data(name)
    wm title . $::data(name)
    set-gain $::data(gain)
    set-freq $::data(freq)

    set row 0
    grid [ttk::label .gain-l -textvar ::data(gain-label) -width 10 -anchor e] -row $row -column 0
    grid [ttk::label .gain-u -text dB] -row $row -column 1
    grid [ttk::scale .gain-s -from $::data(max-gain) -to $::data(min-gain) -command set-gain -variable ::data(gain) -length $::data(length)] -row $row -column 2 -sticky ew
    incr row
    grid [ttk::label .freq-l -textvar ::data(freq-label) -width 10 -anchor e] -row $row -column 0
    grid [ttk::label .freq-u -text Hz] -row $row -column 1
    grid [ttk::scale .freq-s -from $::data(min-freq) -to $::data(max-freq) -command set-freq -variable ::data(freq) -length $::data(length)] -row $row -column 2 -sticky ew

    grid columnconfigure . 2 -weight 100
    bind . <Destroy> [list shutdown %W]
}

main $argv
