#!/usr/bin/tclsh
# -*- mode: Tcl; tab-width: 8; -*-
#
# Copyright (C) 2011, 2012 by Roger E Critchlow Jr, Santa Fe, NM, USA.
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
# 

#
# an sdrkit embeddable iq-delay component
#

#
# find the script file
# and add the lib directory to the automatic package search path
#
set script [expr { ! [catch {file readlink [info script]} link] ? $link : [info script]}]
lappend auto_path [file join [file dirname $script] .. lib]

#
# require Tk for graphical UI and communications
# sdrtcl::iq-delay to implement the component
#
package require Tk
package require sdrtcl::iq-delay

#
# on the fly implementation of control interface
#
package require snit

namespace eval sdrkit {}

snit::type sdrkit::iq-delay {    
    option -server -default default -readonly true
    option -sample-rate -default 0 -readonly true
    option -name -default sdr-iq-delay -readonly true
    option -minsizes -default {100 200} -readonly true
    option -weights -default {1 3} -readonly true
    option -control -default {} -readonly true
    option -enable -default 1 -configuremethod Configure
    option -activate -default 1 -configuremethod Configure

    option -delay -default 0 -configuremethod Configure

    variable data -array {
    }

    constructor {args} {

	$self configure {*}$args

	wm title . $options(-name)
	tk appname $options(-name)

	sdrtcl::iq-delay $options(-name) -server $options(-server)

	if {$options(-control) ne {}} {
	    set options(-enable) 0
	    set options(-activate) 0
	    if {[$options(-name) is-active]} { $options(-name) deactivate }
	} else {
	    set options(-enable) 1
	    set options(-activate) 1
	    if { ! [$options(-name) is-active]} { $options(-name) activate }
	}

	set data(pi) [tcl::mathfunc::atan2 0 -1]

	ttk::frame .delay
	ttk::radiobutton .delay.minus -text "Delay Q" -value -1 -variable [myvar options(-delay)] -command [mymethod Set delay -1]
	ttk::radiobutton .delay.zero -text "No Delay" -value 0 -variable [myvar options(-delay)] -command [mymethod Set delay 0]
	ttk::radiobutton .delay.plus -text "Delay I" -value 1 -variable [myvar options(-delay)] -command [mymethod Set delay 1]
	grid .delay.minus .delay.zero .delay.plus -sticky ew
	grid .delay -sticky ew 

	grid columnconfigure . 0 -minsize [tcl::mathop::+ {*}$options(-minsizes)]
    }
    destructor {
	catch {$options(-name) deactivate}
	catch {rename $options(-name) {}}
    }
    method Configure {opt val} {
	set options($opt) $val
	switch -- $opt {
	    -activate { $options(-name) [expr {$val?{activate}:{deactivate}}] }
	    -delay { $options(-name) configure $opt $val }
	}
    }
    method {Set delay} {v} {
	$options(-name) configure -delay $v
    }
}

sdrkit::iq-delay ::sdrkit {*}$argv
