if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2
    namespace import -force ::tcltest::*
}

package require dbus-tcl
dbus connect

proc handler {data args} {
    set ::result [dict get $data signature],[lindex $args 0]
}

test variant-1.1 {auto guess integer} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo [expr 0x2a]]
    return [lindex $resp 1]
} 42

test variant-1.2 {auto guess double} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo [expr 42e0]]
    return [lindex $resp 1]
} 42.0

test variant-1.3 {auto guess list} {
    set foo {foo  bar  baz}
    llength $foo; # Turn internal representation into a list
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo $foo]
    return [lindex $resp 1]
} {{foo bar baz}}

test variant-1.4 {auto guess dict} {
    set foo {a foo b bar a baz}
    dict size $foo; # Turn internal representation into a dict
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo $foo]
    return [lindex $resp 1]
} {{a baz b bar}}

test variant-1.5 {auto guess string} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo "Hello world"]
    return [lindex $resp 1]
} {{Hello world}}

test variant-2.1 {boolean} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo {b 42}]
    return [lindex $resp 1]
} 1

test variant-2.2 {byte} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo {y 12345}]
    return [lindex $resp 1]
} 57

test variant-2.3 {integer} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo {i 0x12345}]
    return [lindex $resp 1]
} 74565

test variant-2.4 {double} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo {d 42}]
    return [lindex $resp 1]
} 42.0

test variant-2.10 {list of integers} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo {ai {13  0o14  0x15}}]
    return [lindex $resp 1]
} {{13 12 21}}

test variant-2.11 {list of doubles} {
    set resp [dbus call -dest com.tclcode.test.responder -signature v \
      /test com.tclcode.test echo {ad {13  0o14  0x15}}]
    return [lindex $resp 1]
} {{13.0 12.0 21.0}}

test variant-3.1 {variant details} -setup {
    set dbus::variantdetails 1
} -body {
    set resp [dbus call -dest com.tclcode.test.responder \
      -details 1 -signature isbav \
      /test com.tclcode.test vecho 42 foo -1 {{d 3.141500} {y -123}}]
    return $resp
} -cleanup {
    set dbus::variantdetails 0
} -result {{i 42} {s foo} {b 1} {av {{d 3.1415} {i 133}}}}

test variant-3.2 {variant details with handler} -setup {
    set dbus::variantdetails 1
} -body {
    dbus call -dest com.tclcode.test.responder -details 1 -signature ayqv \
      -handler handler /test com.tclcode.test vecho {-1 53 1234} -33333 baz
    set id [after 500 {set result timeout}]
    vwait result
    after cancel $id
    return $result
} -cleanup {
    set dbus::variantdetails 0
} -result {av,{ay {255 53 210}} {q 32203} {v {s baz}}}

# cleanup
cleanupTests
return
